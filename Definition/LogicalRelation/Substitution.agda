{-# OPTIONS --without-K  #-}

open import Definition.Typed.EqualityRelation

module Definition.LogicalRelation.Substitution {{eqrel : EqRelSet}} where

open import Definition.Untyped
open import Definition.Typed
open import Definition.LogicalRelation

open import Tools.Product
open import Tools.Unit


-- The validity judgements:
-- We consider expressions that satisfy these judgments valid

mutual
  -- Validity of contexts
  data âŠ©áµ›_ : Con Term â†’ Set where
    Îµ : âŠ©áµ› Îµ
    _âˆ™_ : âˆ€ {Î“ A sA l} ([Î“] : âŠ©áµ› Î“) â†’ Î“ âŠ©áµ›âŸ¨ l âŸ© A â¦‚ sA / [Î“]
        â†’ âŠ©áµ› Î“ âˆ™ A â¦‚ sA

  -- Validity of types
  _âŠ©áµ›âŸ¨_âŸ©_â¦‚_/_ : (Î“ : Con Term) (l : TypeLevel) (A : Term) â†’ ğ•Š â†’ âŠ©áµ› Î“ â†’ Set
  Î“ âŠ©áµ›âŸ¨ l âŸ© A â¦‚ s / [Î“] = âˆ€ {Î” Ïƒ} (âŠ¢Î” : âŠ¢ Î”) ([Ïƒ] : Î” âŠ©Ë¢ Ïƒ âˆ· Î“ / [Î“] / âŠ¢Î”)
                   â†’ Î£ (Î” âŠ©âŸ¨ l âŸ© subst Ïƒ A â¦‚ s)
                       (Î» [AÏƒ] â†’ âˆ€ {Ïƒâ€²} ([Ïƒâ€²] : Î” âŠ©Ë¢ Ïƒâ€² âˆ· Î“ / [Î“] / âŠ¢Î”)
                               â†’ ([Ïƒâ‰¡Ïƒâ€²] : Î” âŠ©Ë¢ Ïƒ â‰¡ Ïƒâ€² âˆ· Î“ / [Î“] / âŠ¢Î” / [Ïƒ])
                               â†’ Î” âŠ©âŸ¨ l âŸ© subst Ïƒ A â‰¡ subst Ïƒâ€² A â¦‚ s / [AÏƒ])

  -- Logical relation for substitutions from a valid context
  _âŠ©Ë¢_âˆ·_/_/_ : (Î” : Con Term) (Ïƒ : Subst) (Î“ : Con Term) ([Î“] : âŠ©áµ› Î“) (âŠ¢Î” : âŠ¢ Î”)
             â†’ Set
  Î” âŠ©Ë¢ Ïƒ âˆ· .Îµ        / Îµ  / âŠ¢Î”                = âŠ¤
  Î” âŠ©Ë¢ Ïƒ âˆ· .(Î“ âˆ™ A â¦‚ sA) / (_âˆ™_ {Î“} {A} {sA} {l} [Î“] [A]) / âŠ¢Î” =
    Î£ (Î” âŠ©Ë¢ tail Ïƒ âˆ· Î“ / [Î“] / âŠ¢Î”) Î» [tailÏƒ] â†’
    (Î” âŠ©âŸ¨ l âŸ© head Ïƒ âˆ· subst (tail Ïƒ) A â¦‚ sA / projâ‚ ([A] âŠ¢Î” [tailÏƒ]))

  -- Logical relation for equality of substitutions from a valid context
  _âŠ©Ë¢_â‰¡_âˆ·_/_/_/_ : (Î” : Con Term) (Ïƒ Ïƒâ€² : Subst) (Î“ : Con Term) ([Î“] : âŠ©áµ› Î“)
                    (âŠ¢Î” : âŠ¢ Î”) ([Ïƒ] : Î” âŠ©Ë¢ Ïƒ âˆ· Î“ / [Î“] / âŠ¢Î”) â†’ Set
  Î” âŠ©Ë¢ Ïƒ â‰¡ Ïƒâ€² âˆ· .Îµ       / Îµ       / âŠ¢Î”              / [Ïƒ] = âŠ¤
  Î” âŠ©Ë¢ Ïƒ â‰¡ Ïƒâ€² âˆ· .(Î“ âˆ™ A â¦‚ sA) / (_âˆ™_ {Î“} {A} {sA} {l} [Î“] [A]) / âŠ¢Î” / [Ïƒ] =
    (Î” âŠ©Ë¢ tail Ïƒ â‰¡ tail Ïƒâ€² âˆ· Î“ / [Î“] / âŠ¢Î” / projâ‚ [Ïƒ]) Ã—
    (Î” âŠ©âŸ¨ l âŸ© head Ïƒ â‰¡ head Ïƒâ€² âˆ· subst (tail Ïƒ) A â¦‚ sA / projâ‚ ([A] âŠ¢Î” (projâ‚ [Ïƒ])))


-- Validity of terms
_âŠ©áµ›âŸ¨_âŸ©_âˆ·_â¦‚_/_/_ : (Î“ : Con Term) (l : TypeLevel) (t A : Term) (sA : ğ•Š) ([Î“] : âŠ©áµ› Î“)
                 ([A] : Î“ âŠ©áµ›âŸ¨ l âŸ© A â¦‚ sA / [Î“]) â†’ Set
Î“ âŠ©áµ›âŸ¨ l âŸ© t âˆ· A â¦‚ sA / [Î“] / [A] =
  âˆ€ {Î” Ïƒ} (âŠ¢Î” : âŠ¢ Î”) ([Ïƒ] : Î” âŠ©Ë¢ Ïƒ âˆ· Î“ / [Î“] / âŠ¢Î”) â†’
  Î£ (Î” âŠ©âŸ¨ l âŸ© subst Ïƒ t âˆ· subst Ïƒ A â¦‚ sA / projâ‚ ([A] âŠ¢Î” [Ïƒ])) Î» [tÏƒ] â†’
  âˆ€ {Ïƒâ€²} â†’ Î” âŠ©Ë¢ Ïƒâ€² âˆ· Î“ / [Î“] / âŠ¢Î” â†’ Î” âŠ©Ë¢ Ïƒ â‰¡ Ïƒâ€² âˆ· Î“ / [Î“] / âŠ¢Î” / [Ïƒ]
    â†’ Î” âŠ©âŸ¨ l âŸ© subst Ïƒ t â‰¡ subst Ïƒâ€² t âˆ· subst Ïƒ A â¦‚ sA / projâ‚ ([A] âŠ¢Î” [Ïƒ])

-- Validity of type equality
_âŠ©áµ›âŸ¨_âŸ©_â‰¡_â¦‚_/_/_ : (Î“ : Con Term) (l : TypeLevel) (A B : Term) (sA : ğ•Š) ([Î“] : âŠ©áµ› Î“)
                ([A] : Î“ âŠ©áµ›âŸ¨ l âŸ© A â¦‚ sA / [Î“]) â†’ Set
Î“ âŠ©áµ›âŸ¨ l âŸ© A â‰¡ B â¦‚ sA / [Î“] / [A] =
  âˆ€ {Î” Ïƒ} (âŠ¢Î” : âŠ¢ Î”) ([Ïƒ] : Î” âŠ©Ë¢ Ïƒ âˆ· Î“ / [Î“] / âŠ¢Î”)
  â†’ Î” âŠ©âŸ¨ l âŸ© subst Ïƒ A â‰¡ subst Ïƒ B â¦‚ sA / projâ‚ ([A] âŠ¢Î” [Ïƒ])

-- Validity of term equality
_âŠ©áµ›âŸ¨_âŸ©_â‰¡_âˆ·_â¦‚_/_/_ : (Î“ : Con Term) (l : TypeLevel) (t u A : Term) (sA : ğ•Š) ([Î“] : âŠ©áµ› Î“)
                    ([A] : Î“ âŠ©áµ›âŸ¨ l âŸ© A â¦‚ sA / [Î“]) â†’ Set
Î“ âŠ©áµ›âŸ¨ l âŸ© t â‰¡ u âˆ· A â¦‚ sA / [Î“] / [A] =
  âˆ€ {Î” Ïƒ} â†’ (âŠ¢Î” : âŠ¢ Î”) ([Ïƒ] : Î” âŠ©Ë¢ Ïƒ âˆ· Î“ / [Î“] / âŠ¢Î”)
          â†’ Î” âŠ©âŸ¨ l âŸ© subst Ïƒ t â‰¡ subst Ïƒ u âˆ· subst Ïƒ A â¦‚ sA / projâ‚ ([A] âŠ¢Î” [Ïƒ])

-- Valid term equality with validity of its type and terms
record [_âŠ©áµ›âŸ¨_âŸ©_â‰¡_âˆ·_â¦‚_/_] (Î“ : Con Term) (l : TypeLevel)
                       (t u A : Term) (sA : ğ•Š) ([Î“] : âŠ©áµ› Î“) : Set where
  constructor modelsTermEq
  field
    [A]   : Î“ âŠ©áµ›âŸ¨ l âŸ© A â¦‚ sA / [Î“]
    [t]   : Î“ âŠ©áµ›âŸ¨ l âŸ© t âˆ· A â¦‚ sA / [Î“] / [A]
    [u]   : Î“ âŠ©áµ›âŸ¨ l âŸ© u âˆ· A â¦‚ sA / [Î“] / [A]
    [tâ‰¡u] : Î“ âŠ©áµ›âŸ¨ l âŸ© t â‰¡ u âˆ· A â¦‚ sA / [Î“] / [A]

-- Validity of reduction of terms
_âŠ©áµ›_â‡’_âˆ·_â¦‚_/_ : (Î“ : Con Term) (t u A : Term) (sA : ğ•Š) ([Î“] : âŠ©áµ› Î“) â†’ Set
Î“ âŠ©áµ› t â‡’ u âˆ· A â¦‚ sA / [Î“] = âˆ€ {Î” Ïƒ} (âŠ¢Î” : âŠ¢ Î”) ([Ïƒ] : Î” âŠ©Ë¢ Ïƒ âˆ· Î“ / [Î“] / âŠ¢Î”)
                       â†’ Î” âŠ¢ subst Ïƒ t â‡’ subst Ïƒ u âˆ· subst Ïƒ A â¦‚ sA
